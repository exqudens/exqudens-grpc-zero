apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    if (System.getenv().GIT_BRANCH == null) {
        mavenLocal()
    }
    mavenCentral()
}

dependencies {

    compile group: 'io.grpc', name: 'grpc-netty',    version: '1.16.1'
    compile group: 'io.grpc', name: 'grpc-protobuf', version: '1.16.1'
    compile group: 'io.grpc', name: 'grpc-stub',     version: '1.16.1'

}

task sourcesJar(type: Jar, dependsOn: ['classes']) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: ['javadoc']) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task protoc(type:Exec, dependsOn: ['cleanProtoc']) {
    doFirst {
        file(sourceSets.main.java.getSrcDirs().last().getAbsolutePath() + '/generated').mkdir()
    }
    commandLine 'protoc'
    args '-I=' + sourceSets.main.resources.getSrcDirs().last().getAbsolutePath()
    args '--java_out=' + sourceSets.main.java.getSrcDirs().last().getAbsolutePath()
    sourceSets.main.resources.srcDirs().getFiles().each { f ->
        if (f.getName().endsWith(".proto")) {
            args f.getAbsolutePath()
        }
    }
}

task cleanProtoc() {
    doLast {
        delete sourceSets.main.java.getSrcDirs().last().getAbsolutePath() + '/generated'
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

jar {
    manifest {
        attributes 'Project-Group': project.group, 'Project-Name': project.name, 'Project-Version': project.version
    }
}

eclipse {
    jdt {
        sourceCompatibility = property('sourceCompatibility')
        targetCompatibility = property('targetCompatibility')
    }
    project {
        file {
            whenMerged { project ->
                project.buildCommands.clear()
                project.buildCommands.add(new org.gradle.plugins.ide.eclipse.model.BuildCommand('org.eclipse.jdt.core.javabuilder'))
                project.natures.clear()
                project.natures = ['org.eclipse.jdt.core.javanature']
            }
        }
    }
    classpath {
        plusConfigurations = [configurations.compileOnly, configurations.testCompileOnly, configurations.testRuntime]
        minusConfigurations = []
    }
}

tasks['eclipse'].doLast {
    delete '.settings/org.eclipse.wst.common.component'
    delete '.settings/org.eclipse.wst.common.project.facet.core.xml'
}

tasks['cleanEclipse'].doLast {
    delete '.settings'
    delete 'bin'
}

tasks['cleanIdea'].doLast {
    delete '.idea'
}

tasks['cleanIdea'].dependsOn 'cleanIdeaWorkspace'
